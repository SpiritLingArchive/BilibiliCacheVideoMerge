plugins {
    id 'com.android.application'
}

/**
 * ffmpeg核心类型
 */

def ffmpegCoreType = "-1"

//ffmpeg核心依赖
dependencies {
    switch (ffmpegCoreType) {
        case "-1"://全核心
            //RxFFmpeg依赖
            implementation 'com.github.microshow:RxFFmpeg:4.9.0-lite'
            //FFmpegCommand依赖
            implementation 'com.github.AnJoiner:FFmpegCommand:1.2.2-lite'
            break
        case "0":
            //RxFFmpeg依赖
            implementation 'com.github.microshow:RxFFmpeg:4.9.0-lite'
            break
        case "1":
            //FFmpegCommand依赖
            implementation 'com.github.AnJoiner:FFmpegCommand:1.2.2-lite'
            break
        default:
            break
    }
}


android {
    namespace 'com.molihua.hlbmerge'
    compileSdk 33

    defaultConfig {
        applicationId "com.molihua.hlbmerge"
        minSdk 21
        targetSdk 33
        versionCode 49
        versionName "1.6.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //设置BuildConfig.FFMPEG_CORE_TYPE可以在java中调用
            buildConfigField "int", "FFMPEG_CORE_TYPE", ffmpegCoreType
        }
        debug {
            buildConfigField "int", "FFMPEG_CORE_TYPE", ffmpegCoreType
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                //配置编译时忽略java文件
                switch (ffmpegCoreType) {
                    case "-1":
                        break
                    case "0":
                        //RxFFmpeg依赖
                        excludes = [
                                'com/molihua/hlbmerge/ffmpeg/core/impl/FFmpegCommandCore.java',
                                'com/molihua/hlbmerge/ffmpeg/core/impl/FFmpegCommandCallback.java'
                        ]
                        break
                    case "1":
                        //FFmpegCommand依赖
                        excludes = [
                                'com/molihua/hlbmerge/ffmpeg/core/impl/RxFFmpegCore.java',
                                'com/molihua/hlbmerge/ffmpeg/core/impl/RxFFmpegCallback.java'
                        ]
                        break
                    default:
                        break
                }
            }
        }
    }

}


dependencies {
    implementation fileTree(dir: "libs", includes: ["*.aar", "*.jar"])
    //文件选择器(包含权限请求)
    implementation 'io.github.molihuan:pathselector:1.1.14'
    // 腾讯的键值对存储mmkv
    implementation 'com.tencent:mmkv:1.2.14'
    //DKVideoPlayer内部默认使用系统mediaplayer进行解码
    implementation 'xyz.doikki.android.dkplayer:dkplayer-java:3.3.7'
    //DKVideoPlayer UI(主要是使用手势控制视图GestureView)
    implementation 'xyz.doikki.android.dkplayer:dkplayer-ui:3.3.7'
    //bilibili弹幕组件
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.21'
    implementation 'com.github.ctiao:ndkbitmap-x86:0.9.21'
    //XUpdate
    implementation 'com.github.xuexiangjys:XUpdate:2.1.2'
    implementation 'com.github.xuexiangjys.XUpdateAPI:xupdate-easy:1.0.1'
    //XUI
    implementation 'com.github.xuexiangjys:XUI:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    //XUI
    //友盟依赖
    implementation 'com.umeng.umsdk:common:9.5.4'// (必选)
    implementation 'com.umeng.umsdk:asms:1.6.3'// 必选
    implementation 'com.umeng.umsdk:apm:1.8.0'// U-APM产品包依赖(必选)


    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'


}